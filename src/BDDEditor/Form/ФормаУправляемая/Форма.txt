#Область РаботаСФормами

&НаКлиенте
Процедура РазвернутьДерево(Команда)
	ЭтаФорма.Элементы.ОбъемПроекта.Развернуть(ЭтаФорма.Элементы.ОбъемПроекта.ТекущаяСтрока,Истина);
КонецПроцедуры

&НаКлиенте
Процедура СвернутьДерево(Команда)
	ЭтаФорма.Элементы.ОбъемПроекта.Свернуть(ЭтаФорма.Элементы.ОбъемПроекта.ТекущаяСтрока);
КонецПроцедуры

&НаКлиенте
Функция ПроверитьВозможностьПереноса(ПереносимыйЭлемент, Знач НовыйРодитель)

    Пока НЕ НовыйРодитель = Неопределено Цикл
        Если ПереносимыйЭлемент = НовыйРодитель Тогда
            Возврат Ложь;
        КонецЕсли;
        НовыйРодитель = НовыйРодитель.ПолучитьРодителя();
    КонецЦикла;

    Возврат Истина;

КонецФункции

&НаКлиенте
Процедура ОбъемПроектаПроверкаПеретаскивания(Элемент, ПараметрыПеретаскивания, СтандартнаяОбработка, Строка, Поле)
	// Вставить содержимое обработчика.
	//СтандартнаяОбработка = Ложь;
	
	РеквизитДерево = ЭтаФорма.ОбъемПроекта;
	ИДНовыйРодитель = Строка;
	НовыйРодитель = ?(ИДНовыйРодитель = Неопределено, Неопределено, РеквизитДерево.НайтиПоИдентификатору(ИДНовыйРодитель)); 	
	МассивИДПереносимыхЭлементов = ПараметрыПеретаскивания.Значение;

	Для каждого ИДПереносимыйЭлемент из МассивИДПереносимыхЭлементов Цикл
		
		ПереносимыйЭлемент = РеквизитДерево.НайтиПоИдентификатору(ИДПереносимыйЭлемент);
		
		Если НЕ ПроверитьВозможностьПереноса(ПереносимыйЭлемент,НовыйРодитель) Тогда
			ПараметрыПеретаскивания.Действие=ДействиеПеретаскивания.Отмена;
			Прервать;
		КонецЕсли;
	КонецЦикла;
КонецПроцедуры

&НаКлиенте
Функция СкопироватьСтрокуДерева(РеквизитДерево, Приемник, Источник)

    Перем НоваяСтрока, ОбратныйИндекс, КолПодчиненныхСтрок;
    Если Источник = Неопределено Тогда
        Возврат Неопределено;
    КонецЕсли;

    Если Приемник = Неопределено Тогда
        НоваяСтрока = РеквизитДерево.ПолучитьЭлементы().Добавить();
    Иначе
        НоваяСтрока = Приемник.ПолучитьЭлементы().Добавить();
    КонецЕсли;

    ЗаполнитьЗначенияСвойств(НоваяСтрока, Источник);
	НоваяСтрока.ПолныйПуть=Приемник.ПолныйПуть+Источник.Имя;

    КолПодчиненныхСтрок = Источник.ПолучитьЭлементы().Количество();
    Для ОбратныйИндекс = 1 По КолПодчиненныхСтрок Цикл
        ПодчиненнаяСтрока = Источник.ПолучитьЭлементы()[КолПодчиненныхСтрок - ОбратныйИндекс];
        СкопироватьСтрокуДерева(РеквизитДерево, НоваяСтрока, ПодчиненнаяСтрока);
	КонецЦикла;
	Если ЕстьПоддержкаАсинхронныхВызовов Тогда
		НачатьПеремещениеФайла(Новый ОписаниеОповещения("ПеремещениеФайлаЗавершение", ЭтаФорма, Новый Структура("Источник, НоваяСтрока, РеквизитДерево", Источник, НоваяСтрока, РеквизитДерево)), Источник.ПолныйПуть,Приемник.ПолныйПуть+"\"+Источник.Имя);
	Иначе	
		ПереместитьФайл(Источник.ПолныйПуть,Приемник.ПолныйПуть+"\"+Источник.Имя);
	КонецЕсли;	
    Если Источник.ПолучитьРодителя() = Неопределено Тогда
        РеквизитДерево.ПолучитьЭлементы().Удалить(Источник);
    Иначе
        Источник.ПолучитьРодителя().ПолучитьЭлементы().Удалить(Источник);
    КонецЕсли;

    Возврат НоваяСтрока;

КонецФункции

&НаКлиенте
Процедура ПеремещениеФайлаЗавершение(ПеремещенныйФайл, ДополнительныеПараметры) Экспорт
	Источник = ДополнительныеПараметры.Источник;
	НоваяСтрока = ДополнительныеПараметры.НоваяСтрока;
	РеквизитДерево = ДополнительныеПараметры.РеквизитДерево;
КонецПроцедуры	

&НаКлиенте
Процедура ОбъемПроектаПеретаскивание(Элемент, ПараметрыПеретаскивания, СтандартнаяОбработка, Строка, Поле)
	// Вставить содержимое обработчика.
	СтандартнаяОбработка=Ложь; 
	РеквизитДерево = ЭтаФорма.ОбъемПроекта; 
	
	ИДПриемник = Строка; 
	Приемник = ?(ИДПриемник = Неопределено, Неопределено,РеквизитДерево.НайтиПоИдентификатору(ИДПриемник)); 
	МассивИДИсточник = ПараметрыПеретаскивания.Значение; 
	Для Каждого ИДИсточник Из МассивИДИсточник Цикл 
		Источник = РеквизитДерево.НайтиПоИдентификатору(ИДИсточник); 
		Если НЕ Источник.Фича Тогда
			Сообщение = Новый СообщениеПользователю();
			Сообщение.Текст = "Перемещение каталогов запрещено!";
			Сообщение.УстановитьДанные(ЭтотОбъект);
			Сообщение.Сообщить();
			Прервать;
		КонецЕсли;	
		НоваяСтрока = СкопироватьСтрокуДерева(РеквизитДерево, Приемник, Источник); 
		Если Приемник = Неопределено и НоваяСтрока<>Неопределено Тогда 
			Элементы.ОбъемПроекта.Развернуть(НоваяСтрока.ПолучитьИдентификатор(),Истина); 
		КонецЕсли; 
	КонецЦикла; 
	Если НЕ Приемник = Неопределено Тогда 
		Элементы.ОбъемПроекта.Развернуть(ИДПриемник, Истина); 
	КонецЕсли; 
	
	
КонецПроцедуры

#КонецОбласти

#Область РаботаСКаталогами

&НаКлиенте
Процедура ПутьКХранилищуНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	//TODO 
	//Здесь будет выбор места хранения структуры
КонецПроцедуры

&НаКлиенте
Процедура ОбработатьВыборФайлаСтруктуры(ВыбранныеФайлы,ДополнительныеПараметры) Экспорт
	Если ВыбранныеФайлы = Неопределено Тогда
		Возврат;
	КонецЕсли;	
	Объект.СтруктураДляЗагрузки=ВыбранныеФайлы[0];
	Если ЗначениеЗаполнено(Объект.СтруктураДляЗагрузки) Тогда
		ЗаполнитьТЗСбораТребованийИзПапкиТемп(Истина);
		//ПолучитьСтруктуруКаталогов("СписокКомпонент");
		ПолучитьСтруктуруКаталогов("ОбъемПроекта");
	КонецЕсли;	
КонецПроцедуры

&НаКлиенте
Процедура СтруктураДляЗагрузкиНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	Режим = РежимДиалогаВыбораФайла.ВыборКаталога;
	ДиалогОткрытияФайла = Новый ДиалогВыбораФайла(Режим);
	Если ЕстьПоддержкаАсинхронныхВызовов Тогда
		ОписаниеОповещения = Вычислить("Новый ОписаниеОповещения(""ОбработатьВыборФайлаСтруктуры"", ЭтаФорма)");
		Выполнить("ДиалогОткрытияФайла.Показать(ОписаниеОповещения)");
	Иначе	
		Если ДиалогОткрытияФайла.Выбрать() Тогда
			Объект.СтруктураДляЗагрузки = ДиалогОткрытияФайла.Каталог;
			Если ЗначениеЗаполнено(Объект.СтруктураДляЗагрузки) Тогда
				ЗаполнитьТЗСбораТребованийИзПапкиТемп(Истина);
				//ПолучитьСтруктуруКаталогов("СписокКомпонент");
				ПолучитьСтруктуруКаталогов("ОбъемПроекта");
			КонецЕсли;	
		Иначе
			Возврат;
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры

&НаСервере
Функция ПодключитьВнешнююОбработкуСервер(АдресХранилища)
	Возврат ВнешниеОбработки.Подключить(АдресХранилища,,Ложь); 
КонецФункции 

&НаКлиенте
Процедура ОбработкаПослеПомещенияФайла(Результат,АдресХранилища,ВыбранноеИмяФайла,ДополнительныеПараметры) Экспорт
	ИмяОбработки = ПодключитьВнешнююОбработкуСервер(АдресХранилища);
	ДополнительныеПараметры.Вставить("ИмяОбработки",ИмяОбработки);
КонецПроцедуры

&НаКлиенте
Функция ПодключитьВнешнююОбработкуКлиент(ИмяФайла) Экспорт
	ДополнительныеПараметры = Новый Структура;
	Если НЕ ЕстьПоддержкаНемодальныхФорм Тогда
		Оповещение = Вычислить("Новый ОписаниеОповещения(""ОбработкаПослеПомещенияФайла"", ЭтаФорма, ДополнительныеПараметры)");
		Выполнить("НачатьПомещениеФайла(Оповещение,, ИмяФайла, Ложь, УникальныйИдентификатор);");
		
		Возврат ДополнительныеПараметры.ИмяОбработки;
	Иначе
		АдресХранилища = "";
		ПоместитьФайл(АдресХранилища, ИмяФайла, , Ложь, УникальныйИдентификатор);
		//ПодключитьВнешнююОбработку(АдресХранилища);
		Результат = Неопределено;
		ИмяОбработки = ПодключитьВнешнююОбработкуСервер(АдресХранилища);
		Возврат ИмяОбработки;
		
		//ОбработкаПолученияФайлаОбработкиТеста(Результат,АдресХранилища,ИмяФайла,ДополнительныеПараметры);
	КонецЕсли;
КонецФункции 

&НаКлиенте
Процедура СкопироватьДеревоФич(ДеревоТестов)//ДеревоТестов
	//ПрочитатьЗначениеСтрокиДерева(Новый Структура("ДеревоТестов",ДеревоТестов));
	ПрочитатьДеревоФич(Новый Структура("ДеревоТестов",ДеревоТестов));
	
КонецПроцедуры	

&НаСервере
Функция ПрочитатьДеревоФич(СтруктураДерева)
	ДеревоСтруктуры=СтруктураДерева["ДеревоТестов"];
	ДеревоР = РеквизитФормыВЗначение("ДеревоРедактора");
	ДеревоР.Строки.Очистить();
	СтрокаДерева=ДеревоСтруктуры.ПолучитьЭлементы();
	
	ПрочитатьЗначениеСтрокиДерева(СтрокаДерева,ДеревоР.Строки);
	
	ЗначениеВРеквизитФормы(ДеревоР,"ДеревоРедактора");
КонецФункции

&НаСервере
Функция ПрочитатьЗначениеСтрокиДерева(СтрокаДерева,ДеревоР)
	Для Каждого Строка Из СтрокаДерева Цикл

		НовСтр = ДеревоР.Добавить();
		ЗаполнитьЗначенияСвойств(НовСтр,Строка);
		СтрокаДалее=Строка.ПолучитьЭлементы();	
		Если СтрокаДалее.Количество()>0 Тогда
			ПрочитатьЗначениеСтрокиДерева(СтрокаДалее,НовСтр.Строки);
		КонецЕсли;	
		
	КонецЦикла;
	
КонецФункции

&НаКлиенте
Процедура ЗаполнитьДерево(Команда) Экспорт
	ИмяОбработки = ПодключитьВнешнююОбработкуКлиент(Объект.ПутьКVanessaBehavior);
	ФормаОбработки = ПолучитьФорму("ВнешняяОбработка." + ИмяОбработки + ".Форма.УправляемаяФорма",,,Истина,);
	Если ЕстьПоддержкаАсинхронныхВызовов Тогда
		ПоказатьЗначение(Новый ОписаниеОповещения("ЗаполнитьДеревоЗавершение", ЭтаФорма, Новый Структура("ФормаОбработки", ФормаОбработки)), ФормаОбработки);		
		
	Иначе
		ФормаОбработки.Открыть();
		ФормаОбработки.Объект.КаталогИнструментов=Объект.КаталогИнструментов;
		ФормаОбработки.Объект.КаталогФич = Объект.СтруктураДляЗагрузки;
		ФормаОбработки.ЗагрузитьФичи();
		СкопироватьДеревоФич(ФормаОбработки.Объект.ДеревоТестов);
		ФормаОбработки.Закрыть();
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьДеревоЗавершение(ДополнительныеПараметры) Экспорт
	
	ФормаОбработки = ДополнительныеПараметры.ФормаОбработки;
	
	ФормаОбработки.Объект.КаталогИнструментов=Объект.КаталогИнструментов;
	ФормаОбработки.Объект.КаталогФич = Объект.СтруктураДляЗагрузки;
	
	ФормаОбработки.ЗагрузитьФичи();
	СкопироватьДеревоФич(ФормаОбработки.Объект.ДеревоТестов);
//	ФормаОбработки.Закрыть();

КонецПроцедуры

&НаСервереБезКонтекста
Функция РазложитьСтрокуВМассивПодстрок(Знач Строка, Знач Разделитель = ",", Знач ПропускатьПустыеСтроки = Неопределено) Экспорт
	
	Результат = Новый Массив;
	
	// для обеспечения обратной совместимости
	Если ПропускатьПустыеСтроки = Неопределено Тогда
		ПропускатьПустыеСтроки = ?(Разделитель = " ", Истина, Ложь);
		Если ПустаяСтрока(Строка) Тогда 
			Если Разделитель = " " Тогда
				Результат.Добавить("");
			КонецЕсли;
			Возврат Результат;
		КонецЕсли;
	КонецЕсли;
	//
	
	Позиция = Найти(Строка, Разделитель);
	Пока Позиция > 0 Цикл
		Подстрока = Лев(Строка, Позиция - 1);
		Если Не ПропускатьПустыеСтроки Или Не ПустаяСтрока(Подстрока) Тогда
			Результат.Добавить(Подстрока);
		КонецЕсли;
		Строка = Сред(Строка, Позиция + СтрДлина(Разделитель));
		Позиция = Найти(Строка, Разделитель);
	КонецЦикла;
	
	Если Не ПропускатьПустыеСтроки Или Не ПустаяСтрока(Строка) Тогда
		Результат.Добавить(Строка);
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции 

&НаСервереБезКонтекста 
Функция ПолучитьРежимМодальностиКонфигурации()
	Возврат Метаданные.РежимИспользованияМодальности = Метаданные.СвойстваОбъектов.РежимИспользованияМодальности.Использовать;
КонецФункции

&НаКлиенте
Процедура ОбработатьВыборФайла(ВыбранныеФайлы,ДополнительныеПараметры) Экспорт
	ПутьСохранения=ДополнительныеПараметры.Путь;
	Если ВыбранныеФайлы = Неопределено Тогда
		Возврат;
	КонецЕсли;	
	Объект[ПутьСохранения]=ВыбранныеФайлы[0];
	
	Если ДополнительныеПараметры.Свойство("ДальнейшаяПроцедура")<>Ложь Тогда
		Выполнить(ДополнительныеПараметры.ДальнейшаяПроцедура);
	КонецЕсли;	
КонецПроцедуры

&НаКлиенте
Процедура ПутьКVanessaBehaviorНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	// Вставить содержимое обработчика.
	Режим = РежимДиалогаВыбораФайла.Открытие;
	ДиалогОткрытияФайла = Новый ДиалогВыбораФайла(Режим);
	ДиалогОткрытияФайла.Заголовок = "Выберите путь к обработке Vanessa Behavior ";
	ДиалогОткрытияФайла.Фильтр = "Файл (*.epf)|*.epf";
	Если ЕстьПоддержкаАсинхронныхВызовов Тогда
		ОписаниеОповещения = Вычислить("Новый ОписаниеОповещения(""ОбработатьВыборФайла"", ЭтаФорма,Новый Структура(""Путь"", ""ПутьКVanessaBehavior""))");
		Выполнить("ДиалогОткрытияФайла.Показать(ОписаниеОповещения)");
	Иначе	
		Если ДиалогОткрытияФайла.Выбрать() Тогда
			Объект.ПутьКVanessaBehavior = ДиалогОткрытияФайла.ПолноеИмяФайла;
		Иначе
			Возврат;
		КонецЕсли;
	КонецЕсли;  
КонецПроцедуры

&НаКлиенте
Процедура КаталогИнструментовНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	// Вставить содержимое обработчика.
	Режим = РежимДиалогаВыбораФайла.ВыборКаталога;
	ДиалогОткрытияФайла = Новый ДиалогВыбораФайла(Режим);
	ДиалогОткрытияФайла.Заголовок = "Выберите путь к каталогу инструментов ";
	Если ЕстьПоддержкаАсинхронныхВызовов Тогда
		ОписаниеОповещения = Вычислить("Новый ОписаниеОповещения(""ОбработатьВыборФайла"", ЭтаФорма,Новый Структура(""Путь"", ""КаталогИнструментов""))");
		Выполнить("ДиалогОткрытияФайла.Показать(ОписаниеОповещения)");
	Иначе	
		Если ДиалогОткрытияФайла.Выбрать() Тогда
			Объект.КаталогИнструментов = ДиалогОткрытияФайла.Каталог;
		Иначе
			Возврат;
		КонецЕсли;
	КонецЕсли;  
КонецПроцедуры

&НаКлиенте
Процедура ПутьКCommonsНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	// Вставить содержимое обработчика.
	Режим = РежимДиалогаВыбораФайла.ВыборКаталога;
	ДиалогОткрытияФайла = Новый ДиалогВыбораФайла(Режим);
	ДиалогОткрытияФайла.Заголовок = "Выберите путь к каталогу инструментов ";
	Если ЕстьПоддержкаАсинхронныхВызовов Тогда
		ОписаниеОповещения = Вычислить("Новый ОписаниеОповещения(""ОбработатьВыборФайла"", ЭтаФорма,Новый Структура(""Путь"", ""ПутьКCommons""))");
		Выполнить("ДиалогОткрытияФайла.Показать(ОписаниеОповещения)");
	Иначе	
		Если ДиалогОткрытияФайла.Выбрать() Тогда
			Объект.ПутьКCommons = ДиалогОткрытияФайла.Каталог;
		Иначе
			Возврат;
		КонецЕсли;
	КонецЕсли;  
КонецПроцедуры

&НаКлиенте
Процедура КаталогBDDEditorНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	// Вставить содержимое обработчика.
	Режим = РежимДиалогаВыбораФайла.ВыборКаталога;
	ДиалогОткрытияФайла = Новый ДиалогВыбораФайла(Режим);
	ДиалогОткрытияФайла.Заголовок = "Выберите путь к каталогу инструментов ";
	Если ЕстьПоддержкаАсинхронныхВызовов Тогда
		ОписаниеОповещения = Вычислить("Новый ОписаниеОповещения(""ОбработатьВыборФайла"", ЭтаФорма,Новый Структура(""Путь"", ""КаталогBDDEditor""))");
		Выполнить("ДиалогОткрытияФайла.Показать(ОписаниеОповещения)");
	Иначе	
		Если ДиалогОткрытияФайла.Выбрать() Тогда
			Объект.КаталогBDDEditor = ДиалогОткрытияФайла.Каталог;
		Иначе
			Возврат;
		КонецЕсли;
	КонецЕсли;  
КонецПроцедуры

&НаКлиенте
Процедура КаталогVanessaStackНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	// Вставить содержимое обработчика.
	КаталогиИнструментов=Новый СписокЗначений;
	КаталогиИнструментов.Добавить("vanessa-behavior.epf","ПутьКVanessaBehavior");
	КаталогиИнструментов.Добавить("vanessa-stack-commons","ПутьКCommons");
	КаталогиИнструментов.Добавить("vanessa-behavior","КаталогИнструментов");
	КаталогиИнструментов.Добавить("vanessa-bdd-editor","КаталогBDDEditor");
	
	Режим = РежимДиалогаВыбораФайла.ВыборКаталога;
	ДиалогОткрытияФайла = Новый ДиалогВыбораФайла(Режим);
	ДиалогОткрытияФайла.Заголовок = "Выберите путь к каталогу инструментов ";
	Если ЕстьПоддержкаАсинхронныхВызовов Тогда
		ОписаниеОповещения = Вычислить("Новый ОписаниеОповещения(""ОбработатьВыборФайла"", ЭтаФорма,Новый Структура(""Путь,ДальнейшаяПроцедура"", ""КаталогVanessaStack"", ""ЗаполнитьКаталогиИнструментовАсинхронная()""))");
		Выполнить("ДиалогОткрытияФайла.Показать(ОписаниеОповещения)");
	Иначе	
		Если ДиалогОткрытияФайла.Выбрать() Тогда
			Объект.КаталогVanessaStack = ДиалогОткрытияФайла.Каталог;
			Для Каждого Стр из КаталогиИнструментов Цикл
				ЗаполнитьКаталогиИнструментовСинхронная(Стр);
			КонецЦикла;
		Иначе
			Возврат;
		КонецЕсли;
	КонецЕсли; 
КонецПроцедуры	

&НаКлиенте
Процедура ЗаполнитьКаталогиИнструментовСинхронная(Стр) Экспорт
	ИмяФайла=Стр.Значение;
	КудаФайл=Стр.Представление;
	НайденныеФайлы=НайтиФайлы(Объект.КаталогVanessaStack,ИмяФайла,Истина);
	Если НайденныеФайлы.Количество()>0 Тогда
		Объект[КудаФайл]=НайденныеФайлы[0].ПолноеИмя;
	КонецЕсли;
		
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьКаталогиИнструментовАСинхронная() Экспорт
		
КонецПроцедуры

#КонецОбласти

#Область Служебные

&НаСервереБезКонтекста
Функция УзнатьЕстьПоддержкаНемодальныхФорм()
	СистемнаяИнформация = Новый СистемнаяИнформация;
	Массив1 = РазложитьСтрокуВМассивПодстрок(СистемнаяИнформация.ВерсияПриложения,".");
	Массив2 = РазложитьСтрокуВМассивПодстрок("8.3.3.641",".");
	
	Версия1БольшеИлиРавно = Истина;
	Для Ккк = 0 По Массив1.Количество()-1 Цикл
		Элем1 = Массив1.Получить(Ккк);
		Элем2 = Массив2.Получить(Ккк);
		
		Если Число(Элем2) > Число(Элем1) Тогда
			Версия1БольшеИлиРавно = Ложь;
		КонецЕсли;	 
	КонецЦикла;
	
	Рез = Версия1БольшеИлиРавно И Вычислить("Метаданные.РежимИспользованияМодальности <> Метаданные.СвойстваОбъектов.РежимИспользованияМодальности.НеИспользовать");
	
	Возврат Рез;
КонецФункции

&НаСервереБезКонтекста
Функция УзнатьЕстьПоддержкаАсинхронныхВызовов()
	СистемнаяИнформация = Новый СистемнаяИнформация;
	Массив1 = РазложитьСтрокуВМассивПодстрок(СистемнаяИнформация.ВерсияПриложения,".");
	Массив2 = РазложитьСтрокуВМассивПодстрок("8.3.5.1383",".");
	
	Версия1БольшеИлиРавно = Истина;
	Для Ккк = 0 По Массив1.Количество()-1 Цикл
		Элем1 = Массив1.Получить(Ккк);
		Элем2 = Массив2.Получить(Ккк);
		
		Если Число(Элем2) > Число(Элем1) Тогда
			Версия1БольшеИлиРавно = Ложь;
		КонецЕсли;	 
	КонецЦикла;
	
	
	Рез = Версия1БольшеИлиРавно И Вычислить("Метаданные.РежимИспользованияСинхронныхВызововРасширенийИВнешнихКомпонент <> Метаданные.СвойстваОбъектов.РежимИспользованияСинхронныхВызововРасширенийИВнешнихКомпонент.Использовать");
	
	Возврат Рез;
КонецФункции

&НаКлиенте
Функция УзнатьЕстьПоддержкаНемодальныхФормКлиент() Экспорт
	Возврат УзнатьЕстьПоддержкаНемодальныхФорм();
КонецФункции

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	//Вставить содержимое обработчика
	ЕстьПоддержкаНемодальныхФорм    = УзнатьЕстьПоддержкаНемодальныхФорм();
	ЕстьПоддержкаАсинхронныхВызовов = УзнатьЕстьПоддержкаАсинхронныхВызовов();
	ПоказыватьРасшифровку=Истина;
	//УсловноеОформление();
//	ИмяОбработки = РеквизитФормыВЗначение("Объект").Метаданные().ПолноеИмя();
КонецПроцедуры

&НаСервере
Процедура ВосстановитьНастройки() Экспорт
	Если Объект.РежимСамотестирования Тогда
		Возврат;
	КонецЕсли;          
	
	Настройки = ХранилищеОбщихНастроек.Загрузить("VanessaBDDEditor");
	Если ТипЗнч(Настройки) = Тип("Структура") Тогда
		
		Настройки.Свойство("КаталогVanessaStack", Объект.КаталогVanessaStack);
		Настройки.Свойство("ПутьКVanessaBehavior", Объект.ПутьКVanessaBehavior);
		Настройки.Свойство("ПутьКCommons",Объект.ПутьКCommons);
		Настройки.Свойство("КаталогИнструментов", Объект.КаталогИнструментов);
		Настройки.Свойство("КаталогBDDEditor", Объект.КаталогBDDEditor);
		
//		Настройки.Свойство("ПоказыватьРасшифровку", Объект.ПоказыватьРасшифровку);
		Настройки.Свойство("СтруктураДляЗагрузки", Объект.СтруктураДляЗагрузки);
		ТекущаяСтраница="";
		Если Настройки.Свойство("ТекущаяСтраница",ТекущаяСтраница) Тогда
			ЭтаФорма.Элементы.ГруппаОбщая.ТекущаяСтраница=ЭтаФорма.Элементы[ТекущаяСтраница];
		КонецЕсли;	
//		Если НЕ ЗначениеЗаполнено(Объект.СтруктураДляЗагрузки) Тогда
			Объект.ПоказыватьРасшифровку=Истина;
//		КонецЕсли;	
		
	КонецЕсли;
	
	Настройки = ХранилищеОбщихНастроек.Загрузить("VanessaEpics");
	Если ТипЗнч(Настройки) = Тип("Структура") Тогда
		Настройки.Свойство("gitrepos", Объект.Репозиторий);
		АктивироватьСтраницыРаботыСРепозиторием();
	Иначе	
		АктивироватьСтраницыНастройки();
	КонецЕсли;

КонецПроцедуры

&НаСервере
Процедура СохранитьНастройки() Экспорт
	Если Объект.РежимСамотестирования Тогда
		Возврат;
	КонецЕсли;  
	                           
	Настройки = Новый Структура;
	Настройки.Вставить("КаталогVanessaStack", Объект.КаталогVanessaStack);
	Настройки.Вставить("ПутьКVanessaBehavior", Объект.ПутьКVanessaBehavior);
	Настройки.Вставить("ПутьКCommons",Объект.ПутьКCommons);
	Настройки.Вставить("КаталогИнструментов", Объект.КаталогИнструментов);
	Настройки.Вставить("КаталогBDDEditor", Объект.КаталогBDDEditor);
	
//	Настройки.Вставить("ПоказыватьРасшифровку", Объект.ПоказыватьРасшифровку);
	Настройки.Вставить("СтруктураДляЗагрузки", Объект.СтруктураДляЗагрузки);
	Настройки.Вставить("ТекущаяСтраница",ЭтаФорма.Элементы.ГруппаОбщая.ТекущаяСтраница.Имя);
	
	ХранилищеОбщихНастроек.Сохранить("VanessaBDDEditor",, Настройки);
КонецПроцедуры

&НаКлиенте
Процедура НастроитьВидимостьИДоступность()
	ЭтаФорма.Элементы.ТЗСбораТребованийРасшифровка.Видимость=Объект.ПоказыватьРасшифровку;
КонецПроцедуры	

&НаКлиенте
Процедура УстановитьНастройкиСамотестирования() Экспорт
	ВременныйКаталог=КаталогВременныхФайлов()+"Temp";
	Темп=Новый Файл(ВременныйКаталог);
	Если НЕ Темп.Существует() Тогда
		СоздатьКаталог(ВременныйКаталог);
	КонецЕсли;	
    Объект.СтруктураДляЗагрузки=ВременныйКаталог;
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	//Вставить содержимое обработчика
	Если Объект.РежимСамотестирования Тогда
		УстановитьНастройкиСамотестирования();
	КонецЕсли;	
		
	ВосстановитьНастройки();
	НастроитьВидимостьИДоступность();
	Если ЗначениеЗаполнено(Объект.СтруктураДляЗагрузки) Тогда
		//ПолучитьСтруктуруКаталогов("СписокКомпонент");
		ПолучитьСтруктуруКаталогов("ОбъемПроекта");
		ЗаполнитьТЗСбораТребованийИзПапкиТемп();	
	КонецЕсли;	
	ЗаполнитьКонтекстноеМеню();
КонецПроцедуры

&НаКлиенте
Процедура ПриЗакрытии()
	//Вставить содержимое обработчика
	СохранитьНастройки();
КонецПроцедуры

&НаСервереБезКонтекста
Функция ПолучитьЗначениеКонстанты(ИмяКонстанты) Экспорт
	ЗначениеКонстанты = Константы[ИмяКонстанты].Получить();
	Возврат ЗначениеКонстанты;
КонецФункции

#КонецОбласти

#Область КонтекстноеМеню

&НаСервере 
Процедура ЗаполнитьКонтекстноеМеню()
	ПунктРД1=Элементы.Добавить("ДеревоРедактораКонтекстноеМенюПунктВыполнитьСценарий", Тип("КнопкаФормы"), Элементы.ДеревоРедактора.КонтекстноеМеню);
	ПунктРД1.Заголовок="Открыть feature файл в редакторе";
	ПунктРД1.ИмяКоманды="ОткрытьФичаФайлВРедакторе";
	
	ПунктРД2=Элементы.Добавить("ДеревоРедактораКонтекстноеМенюПунктОткрытьФичаФайл", Тип("КнопкаФормы"), Элементы.ДеревоРедактора.КонтекстноеМеню);
	ПунктРД2.Заголовок="Открыть feature файл в Notepad++";
	ПунктРД2.ИмяКоманды="ОткрытьФичаФайлВNotepad";
	
	ПунктОП1=Элементы.Добавить("ОбъемПроектаКонтекстноеМенюПунктВыполнитьСценарий", Тип("КнопкаФормы"), Элементы.ОбъемПроекта.КонтекстноеМеню);
	ПунктОП1.Заголовок="Открыть feature файл в редакторе";
	ПунктОП1.ИмяКоманды="ОткрытьФичаФайлВРедакторе";
	
	ПунктОП2=Элементы.Добавить("ОбъемПроектаКонтекстноеМенюПунктОткрытьФичаФайл", Тип("КнопкаФормы"), Элементы.ОбъемПроекта.КонтекстноеМеню);
	ПунктОП2.Заголовок="Открыть feature файл в Notepad++";
	ПунктОП2.ИмяКоманды="ОткрытьФичаФайлВNotepad";
КонецПроцедуры

&НаКлиенте
Процедура ОбработатьЗапускПриложения(КодВозврата,ДополнительныеПараметры) Экспорт
КонецПроцедуры

&НаКлиенте
Функция НайтиСтрокуФичиЧерезРодителя(Стр)
	СтрокаДерева = Стр;
	Пока Истина Цикл
		СтрокаДерева = СтрокаДерева.ПолучитьРодителя();
		Если СтрокаДерева = Неопределено Тогда
			Возврат Неопределено;
		КонецЕсли;	 
		
		Если СтрокаДерева.Фича = Истина Тогда
			Возврат СтрокаДерева;
		КонецЕсли;	 
	КонецЦикла;
КонецФункции	

&НаКлиенте
Процедура ОткрытьФичаФайлВРедакторе(Команда)
	СтрокаФичи = Элементы[этаформа.ТекущийЭлемент.Имя].ТекущиеДанные;
	Если СтрокаФичи.Фича <> Истина Тогда
		СтрокаФичи = НайтиСтрокуФичиЧерезРодителя(СтрокаФичи);
		Если СтрокаФичи = Неопределено Тогда
			Сообщить("Строка с фича-файлом не найдена!");
			Возврат;
		КонецЕсли;	 
	КонецЕсли;	 
	
	ФормаОбработки = ПолучитьФорму("ВнешняяОбработка.BDDEditor.Форма.ФормаРедактора",,ЭтаФорма);
	СписокОткрытыхОкон=ПолучитьОкна();
	//Для Каждого Окно из СписокОткрытыхОкон Цикл
	//	
	//КонецЦикла;
	Если ФормаОбработки.Открыта() Тогда
		Если ФормаОбработки.ПутьКФиче<>СтрокаФичи.ПолныйПуть Тогда// И ФормаОбработки.БылиИзменения Тогда
			ФормаОбработки.ЗаписатьИзмененияВФиче();
			ФормаОбработки.ПутьКФиче=СтрокаФичи.ПолныйПуть;
			ФормаОбработки.ЕстьПоддержкаАсинхронныхВызовов=ЭтаФорма.ЕстьПоддержкаАсинхронныхВызовов;
			ФормаОбработки.ЕстьПоддержкаНемодальныхФорм=ЭтаФорма.ЕстьПоддержкаНемодальныхФорм;
			ФормаОбработки.КаталогBDDEditor=Объект.КаталогBDDEditor;
			ФормаОбработки.ПолучитьТекстФичи();
			ФормаОбработки.Активизировать();
			ФормаОбработки.Открыть();
		Иначе	
			ФормаОбработки.ПутьКФиче=СтрокаФичи.ПолныйПуть;
			ФормаОбработки.ЕстьПоддержкаАсинхронныхВызовов=ЭтаФорма.ЕстьПоддержкаАсинхронныхВызовов;
			ФормаОбработки.ЕстьПоддержкаНемодальныхФорм=ЭтаФорма.ЕстьПоддержкаНемодальныхФорм;
			ФормаОбработки.КаталогBDDEditor=Объект.КаталогBDDEditor;
			ФормаОбработки.ПолучитьТекстФичи();
			ФормаОбработки.Открыть();
		КонецЕсли;	
	Иначе	
		ФормаОбработки.ПутьКФиче=СтрокаФичи.ПолныйПуть;
		ФормаОбработки.ЕстьПоддержкаАсинхронныхВызовов=ЭтаФорма.ЕстьПоддержкаАсинхронныхВызовов;
		ФормаОбработки.ЕстьПоддержкаНемодальныхФорм=ЭтаФорма.ЕстьПоддержкаНемодальныхФорм;
		ФормаОбработки.КаталогBDDEditor=Объект.КаталогBDDEditor;
		ФормаОбработки.Открыть();
	КонецЕсли;	
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьФичаФайлВNotepad(Команда)
	СтрокаФичи = Элементы[этаформа.ТекущийЭлемент.Имя].ТекущиеДанные;
	Если СтрокаФичи.Фича <> Истина Тогда
		СтрокаФичи = НайтиСтрокуФичиЧерезРодителя(СтрокаФичи);
		Если СтрокаФичи = Неопределено Тогда
			Сообщить("Строка с фича-файлом не найдена!");
			Возврат;
		КонецЕсли;	 
	КонецЕсли;	 
	
	Если ЕстьПоддержкаАсинхронныхВызовов Тогда
		ОписаниеОповещения = Вычислить("Новый ОписаниеОповещения(""ОбработатьЗапускПриложения"", ЭтаФорма)");
		Выполнить("НачатьЗапускПриложения(ОписаниеОповещения,СтрокаФичи.ПолныйПуть)");
	Иначе	
		ЗапуститьПриложение(СтрокаФичи.ПолныйПуть);
	КонецЕсли;  
	
КонецПроцедуры

#КонецОбласти

#Область Фичи

&НаКлиенте
Функция УбратьЗапрещенныеСимволы(ИмяФайла) Экспорт
	ИмяФайла = СтрЗаменить(ИмяФайла,".","");
	ИмяФайла = СтрЗаменить(ИмяФайла,",","");
	ИмяФайла = СтрЗаменить(ИмяФайла,":","");
	ИмяФайла = СтрЗаменить(ИмяФайла,";","");
	ИмяФайла = СтрЗаменить(ИмяФайла,"-","_");
	ИмяФайла = СтрЗаменить(ИмяФайла,"+","");
	ИмяФайла = СтрЗаменить(ИмяФайла,"/","");
	ИмяФайла = СтрЗаменить(ИмяФайла,"\","");
	ИмяФайла = СтрЗаменить(ИмяФайла,"=","");
	ИмяФайла = СтрЗаменить(ИмяФайла,"!","");
	ИмяФайла = СтрЗаменить(ИмяФайла,"@","");
	ИмяФайла = СтрЗаменить(ИмяФайла,"#","");
	ИмяФайла = СтрЗаменить(ИмяФайла,"$","");
	ИмяФайла = СтрЗаменить(ИмяФайла,"%","");
	ИмяФайла = СтрЗаменить(ИмяФайла,"^","");
	ИмяФайла = СтрЗаменить(ИмяФайла,"&","");
	ИмяФайла = СтрЗаменить(ИмяФайла,"*","");
	ИмяФайла = СтрЗаменить(ИмяФайла,"(","");
	ИмяФайла = СтрЗаменить(ИмяФайла,")","");
	ИмяФайла = СтрЗаменить(ИмяФайла,"№","");
	ИмяФайла = СтрЗаменить(ИмяФайла,"?","");
	ИмяФайла = СтрЗаменить(ИмяФайла,"`","");
	ИмяФайла = СтрЗаменить(ИмяФайла,"'","");
	ИмяФайла = СтрЗаменить(ИмяФайла,"~","");
	ИмяФайла = СтрЗаменить(ИмяФайла,"<","");
	ИмяФайла = СтрЗаменить(ИмяФайла,">","");
	Возврат ИмяФайла;
КонецФункции

&НаКлиенте
Функция СделатьПервуюБуквуЗаглавной(Стр) Экспорт
	Если СтрДлина(Стр) = 0 Тогда
		Возврат Стр;
	КонецЕсли;
	
	ПерваяБуква = ВРег(Лев(Стр,1));
	
	Возврат ПерваяБуква + Сред(Стр,2);
КонецФункции

&НаКлиенте
Функция СформироватьИмяФайла(ИмяФайла) Экспорт
	ИмяФайла=УбратьЗапрещенныеСимволы(ИмяФайла);
	МассивПодстрок = РазложитьСтрокуВМассивПодстрок(ИмяФайла," ",Истина);
	НовоеИмяФайла="";
	Для Каждого Элем Из МассивПодстрок Цикл
		НовоеИмяФайла = НовоеИмяФайла + СделатьПервуюБуквуЗаглавной(Элем);
	КонецЦикла;
	Возврат НовоеИмяФайла;
КонецФункции	

&НаКлиенте
Процедура СоздатьФичаФайлыНаСервере()
	// Вставить содержимое обработчика.
	
	
КонецПроцедуры

&НаКлиенте
Функция ПоискФайлаПоИмени(ИмяФайла,ТекСтрока,ПараметрыПутей) Экспорт
	Вернуть=Новый Массив;
	НайденныеФайлы = Неопределено;
	
	Если ЕстьПоддержкаАсинхронныхВызовов Тогда
		НачатьПоискФайлов(Новый ОписаниеОповещения("ПоискФайлаПоИмениЗавершение", ЭтаФорма, Новый Структура("Вернуть,ТекСтрока,ПараметрыПутей", Вернуть,ТекСтрока,ПараметрыПутей)), Объект.СтруктураДляЗагрузки,ИмяФайла,Истина);
		
	Иначе
		
		НайденныеФайлы=НайтиФайлы(Объект.СтруктураДляЗагрузки,ИмяФайла,Истина);
		Для Каждого Файлик из НайденныеФайлы Цикл
			Если Файлик.ПолноеИмя<>Неопределено Тогда
				Вернуть.Добавить(Файлик.ПолноеИмя);
			КонецЕсли;
		КонецЦикла;	
		ОбработатьНайденныеФичи(Вернуть,ТекСтрока,ПараметрыПутей);
	КонецЕсли;
КонецФункции	

&НаКлиенте
Процедура ПоискФайлаПоИмениЗавершение(НайденныеФайлы1, ДополнительныеПараметры) Экспорт
	
	Вернуть = ДополнительныеПараметры.Вернуть;
	ТекСтрока = ДополнительныеПараметры.ТекСтрока;
	ПараметрыПутей = ДополнительныеПараметры.ПараметрыПутей;
	НайденныеФайлы=НайденныеФайлы1;
	Для Каждого Файлик из НайденныеФайлы Цикл
		Если Файлик.ПолноеИмя<>Неопределено Тогда
			Вернуть.Добавить(Файлик.ПолноеИмя);
		КонецЕсли;
	КонецЦикла;	
	ОбработатьНайденныеФичи(Вернуть,ТекСтрока,ПараметрыПутей);
	//Возврат Вернуть;

КонецПроцедуры	

&НаКлиенте
Процедура ОбработатьНайденныеФичи(НайденныеФайлы,ТекСтрока,ПараметрыПутей)
	ПутьКФайлу=ПараметрыПутей.ПутьКФайлу;
	ПутьКФайлуСТемп=ПараметрыПутей.ПутьКФайлуСТемп;
	НовоеИмяФайла=ПараметрыПутей.НовоеИмяФайла;
	Стр=ПараметрыПутей.Стр;
	Если НайденныеФайлы.Количество()=0 Тогда
		Если ЗначениеЗаполнено(ТекСтрока.Компонент) Тогда
			ПолныйПуть=""+ПутьКФайлу+"\"+СокрЛП(ТекСтрока.Компонент)+"\"+СокрЛП(НовоеИмяФайла);
		Иначе
			ПолныйПуть=""+СокрЛП(ПутьКФайлуСТемп)+СокрЛП(НовоеИмяФайла);
		КонецЕсли;	
		
		АдресФайлаВоВременномХранилище = "";
		
		Шаблон=ПолучитьШаблонФичаФайла("ШаблонФичи",Стр, АдресФайлаВоВременномХранилище);
		
		Если Не ПустаяСтрока(АдресФайлаВоВременномХранилище) Тогда
			ПолучитьФайл(АдресФайлаВоВременномХранилище, ПолныйПуть,Ложь);
		КонецЕсли;
		ТекСтрока.ФичаФайл=ПолныйПуть;
	ИначеЕсли НайденныеФайлы.Количество()=1 Тогда
		ПолныйПуть=НайденныеФайлы[0];
		ТекСтрока.ФичаФайл=ПолныйПуть;
	Иначе 
		//Элементы.ТЗСбораТребованийЕстьФичи.Видимость=Истина;
		ТекСтрока.ЕстьФичи="Существует несколько feature-файлов с таким именем";
	КонецЕсли;	
КонецПроцедуры

&НаКлиенте
Процедура СоздатьКаталогТемп(Существует, ДополнительныеПараметры) Экспорт
	
	ПутьКФайлу = ДополнительныеПараметры.ПутьКФайлу;
	ПутьКФайлуСТемп = ДополнительныеПараметры.ПутьКФайлуСТемп;
	
	Если НЕ Существует Тогда
		НачатьСозданиеКаталога(Новый ОписаниеОповещения("СоздатьФичаФайлыЗавершение", ЭтаФорма, Новый Структура("ПутьКФайлу, ПутьКФайлуСТемп", ПутьКФайлу, ПутьКФайлуСТемп)), ПутьКФайлуСТемп);
		Возврат;
	КонецЕсли;	
	СоздатьФичаФайлыФрагмент(ПутьКФайлу, ПутьКФайлуСТемп);

КонецПроцедуры

&НаКлиенте
Процедура СоздатьФичаФайлыЗавершение(ПутьКФайлуСТемп, ДополнительныеПараметры) Экспорт
	
	ПутьКФайлу = ДополнительныеПараметры.ПутьКФайлу;
	ПутьКФайлуСТемп = ДополнительныеПараметры.ПутьКФайлуСТемп;
	
	СоздатьФичаФайлыФрагмент(ПутьКФайлу, ПутьКФайлуСТемп);

КонецПроцедуры

&НаКлиенте
Процедура ПроверитьНаличиеПапкиТемп(Файл, ДополнительныеПараметры) Экспорт
	
	ПутьКФайлу = ДополнительныеПараметры.ПутьКФайлу;
	ПутьКФайлуСТемп = ДополнительныеПараметры.ПутьКФайлуСТемп;
	ДальнейшаяПроцедура = ДополнительныеПараметры.ДальнейшаяПроцедура;
	
	Темп=Файл;
	Темп.НачатьПроверкуСуществования(Новый ОписаниеОповещения(ДальнейшаяПроцедура, ЭтаФорма, ДополнительныеПараметры));

КонецПроцедуры

&НаКлиенте
Процедура СоздатьФичаФайлы(Команда)
	Если НЕ ЗначениеЗаполнено(ЭтаФорма.Объект.СтруктураДляЗагрузки) Тогда
		Сообщить("Не заполнен путь к структуре!");
		Возврат;
	КонецЕсли;	
	ПутьКФайлу = Объект.СтруктураДляЗагрузки;
	ПутьКФайлуСТемп=Объект.СтруктураДляЗагрузки+"\Drafts";
	Если ЕстьПоддержкаАсинхронныхВызовов Тогда
		Темп = Новый Файл();
		Темп.НачатьИнициализацию(Новый ОписаниеОповещения("ПроверитьНаличиеПапкиТемп", ЭтаФорма, Новый Структура("ПутьКФайлу, ПутьКФайлуСТемп, ДальнейшаяПроцедура", ПутьКФайлу, ПутьКФайлуСТемп,"СоздатьКаталогТемп")), ПутьКФайлуСТемп);
	Иначе
		Темп=Новый Файл(ПутьКФайлуСТемп);
		Если НЕ Темп.Существует() Тогда
			СоздатьКаталог(ПутьКФайлуСТемп);
		КонецЕсли;	
		ПутьКФайлуСТемп=ПутьКФайлуСТемп;//+"\";
		СоздатьФичаФайлыФрагмент(ПутьКФайлу, ПутьКФайлуСТемп);
		
	КонецЕсли;	

КонецПроцедуры

&НаКлиенте
Процедура СоздатьФичаФайлыФрагмент(Знач ПутьКФайлу, ПутьКФайлуСТемп) Экспорт
	
	Перем АдресФайлаВоВременномХранилище, ИмяФайла, НайденныеФайлы, НовоеИмяФайла, ПолныйПуть, Стр, ТекСтрока, Шаблон;
	
	ПутьКФайлуСТемп=ПутьКФайлуСТемп+"\";
	
	Для Стр=0 По ТЗСбораТребований.Количество()-1 Цикл
		ТекСтрока=ТЗСбораТребований.Получить(Стр);
		Если ЗначениеЗаполнено(ТекСтрока.ФичаФайл) Тогда
			Продолжить;
		КонецЕсли;
		Если НЕ ЗначениеЗаполнено(ТекСтрока.Функционал) Тогда
			Продолжить;
		КонецЕсли;	
		ИмяФайла = СокрЛП(ТекСтрока.Функционал);
		НовоеИмяФайла="";
		НовоеИмяФайла = СформироватьИмяФайла(ИмяФайла);
		НовоеИмяФайла=НовоеИмяФайла+".feature";
		НайденныеФайлы=Новый Структура;
		ПараметрыПутей=Новый Структура;
		ПараметрыПутей.Вставить("ПутьКФайлу",ПутьКФайлу);
		ПараметрыПутей.Вставить("ПутьКФайлуСТемп",ПутьКФайлуСТемп);
		ПараметрыПутей.Вставить("НовоеИмяФайла",НовоеИмяФайла);
		ПараметрыПутей.Вставить("Стр",Стр);
		НайденныеФайлы=ПоискФайлаПоИмени(НовоеИмяФайла,ТекСтрока,ПараметрыПутей);
		
	КонецЦикла;

КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьИзТемпа(Команда) Экспорт
	// Вставить содержимое обработчика.
	Если НЕ ЗначениеЗаполнено(ЭтаФорма.Объект.СтруктураДляЗагрузки) Тогда
		Сообщить("Не заполнен путь к структуре!");
		Возврат;
	КонецЕсли;	
	ЗаполнитьТЗСбораТребованийИзПапкиТемп(Истина);	
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьТЗСбораТребованийИзПапкиТемп(Обновлять=Ложь) Экспорт
	ПутьКФайлу = Объект.СтруктураДляЗагрузки;
	ПутьКФайлуСТемп=Объект.СтруктураДляЗагрузки+"\Drafts";
	ТЗСбораТребований.Очистить();
	//Элементы.ТЗСбораТребованийЕстьФичи.Видимость=Ложь;
	Элементы.Функционал.ЦветФона=Новый Цвет(255,255,255);
	
	Если ЕстьПоддержкаАсинхронныхВызовов Тогда
		Темп = Новый Файл();
		Темп.НачатьИнициализацию(Новый ОписаниеОповещения("ПроверитьНаличиеПапкиТемп", ЭтаФорма, Новый Структура("ПутьКФайлу, ПутьКФайлуСТемп, ДальнейшаяПроцедура,Обновлять", ПутьКФайлу, ПутьКФайлуСТемп,"ЗаполнитьФичамиИзКаталога",Обновлять)), ПутьКФайлуСТемп);
	Иначе
		Темп=Новый Файл(ПутьКФайлуСТемп);
		Если НЕ Темп.Существует() Тогда
			СоздатьКаталог(ПутьКФайлуСТемп);
			Возврат;
		КонецЕсли;	
		ПутьКФайлуСТемп=ПутьКФайлуСТемп+"\";
		НайденныеФайлы=НайтиФайлы(ПутьКФайлуСТемп,"*.feature",Ложь);
		ДополнительныеПараметры=Новый Структура("Обновлять",Обновлять);
		ЗаполнитьФичаФайламиИзВременногоКаталога(НайденныеФайлы, ДополнительныеПараметры);
	КонецЕсли;	
	
КонецПроцедуры	

&НаКлиенте
Процедура ЗаполнитьФичамиИзКаталога(Существует, ДополнительныеПараметры) Экспорт
	
	ПутьКФайлуСТемп = ДополнительныеПараметры.ПутьКФайлуСТемп;
	
	Если Существует Тогда 
		НачатьПоискФайлов(Новый ОписаниеОповещения("ЗаполнитьФичаФайламиИзВременногоКаталога",ЭтаФорма,ДополнительныеПараметры),ПутьКФайлуСТемп,"*.feature",Ложь);
//		НачатьПолучениеФайлов(Новый ОписаниеОповещения("ЗаполнитьФичаФайламиИзВременногоКаталога", ЭтаФорма, Новый Структура("ПутьКФайлу, ПутьКФайлуСТемп", ПутьКФайлу, ПутьКФайлуСТемп)), ПутьКФайлуСТемп);
		Возврат;
	КонецЕсли;	
	
КонецПроцедуры


&НаКлиенте
Процедура ЗаполнитьФичаФайламиИзВременногоКаталога(НайденныеФайлы, ДополнительныеПараметры) Экспорт
	Обновлять=ДополнительныеПараметры.Обновлять;
	Для Каждого Файл из НайденныеФайлы Цикл
		НашлиСТроки=ЭтотОбъект.ТЗСбораТребований.НайтиСтроки(Новый Структура("ФичаФайл",Файл.ПолноеИмя));
		Если НашлиСтроки.Количество()<>0 и НЕ Обновлять Тогда
			Продолжить;
		ИначеЕсли НашлиСтроки.Количество()<>0 и Обновлять Тогда
			Строка= НашлиСтроки[0];
		Иначе
			Строка=ЭтотОбъект.ТЗСбораТребований.Добавить();
		КонецЕсли;	
		Строка.ФичаФайл=Файл.ПолноеИмя;
		Строка.Функционал=Файл.ИмяБезРасширения;
		СтруктураФичи=РаспарситьФункционалОднойФичи(Файл.ПолноеИмя);
		Строка.Функционал=СтруктураФичи["Функционал"];
		Строка.Роль=СтруктураФичи["Роль"];
		Строка.Описание=СтруктураФичи["Описание"];
		Строка.Цель=СтруктураФичи["Цель"];
		Строка.КоличествоСценариев=СтруктураФичи["КоличествоСценариев"];
	КонецЦикла;	
	
КонецПроцедуры	

&НаКлиенте
Функция СоздатьСтруктуруФичаФайла()
	СтруктураФичи=Новый Структура;
	СтруктураФичи.Вставить("Функционал");
	СтруктураФичи.Вставить("Роль");
	СтруктураФичи.Вставить("Описание");
	СтруктураФичи.Вставить("Цель");
	СтруктураФичи.Вставить("КоличествоСценариев");
	Возврат СтруктураФичи;
КонецФункции	

&НаКлиенте
Функция РаспарситьФункционалОднойФичи(ПутьКФиче) Экспорт
	
	СтруктураФичи=СоздатьСтруктуруФичаФайла();
	ИмяФайлаФичи = ПутьКФиче;
	ТекстФичи="";
	Текст = Новый ЧтениеТекста;
	
	Текст.Открыть(ИмяФайлаФичи,"UTF-8");
	Стр=Текст.Прочитать();
	КоличествоСценариев=СтрЧислоВхождений(Стр,"Сценарий:");
	СтруктураФичи.Вставить("КоличествоСценариев",КоличествоСценариев);
	Текст.Закрыть();
	
	
	Текст.Открыть(ИмяФайлаФичи,"UTF-8");
	
	Стр = Текст.ПрочитатьСтроку();
	Пока Стр <> Неопределено Цикл // строки читаются до символа перевода строки
		Стр=СокрЛП(Стр);
		Если СтрНачинаетсяС(Стр,"Контекст") Тогда
			Прервать;
		ИначеЕсли СтрНачинаетсяС(Стр,"Функционал") Тогда
			Функционал=СокрЛП(Сред(Стр,СтрДлина("Функционал:")+1));
			СтруктураФичи.Вставить("Функционал",Функционал);
		ИначеЕсли СтрНачинаетсяС(Стр,"Как") Тогда
			Роль=СокрЛП(Сред(Стр,СтрДлина("Как")+1));
			СтруктураФичи.Вставить("Роль",Роль);
		ИначеЕсли СтрНачинаетсяС(Стр,"Я хочу") Тогда
			Описание=СокрЛП(Сред(Стр,СтрДлина("Я хочу")+1));
			//Описание=СокрЛП(Стр);
			СтруктураФичи.Вставить("Описание",Описание);
		ИначеЕсли СтрНачинаетсяС(Стр,"Чтобы") Тогда
			Цель=СокрЛП(Сред(Стр,СтрДлина("Чтобы")+1));
			//Цель=СокрЛП(Стр);
			СтруктураФичи.Вставить("Цель",Цель);
		КонецЕсли;	
		Стр = Текст.ПрочитатьСтроку();
	КонецЦикла;	
	Текст.Закрыть();
	Возврат СтруктураФичи;
		
	
КонецФункции

&НаСервере
Функция ПолучитьШаблонФичаФайла(ИмяМакета,ИндСтроки,АдресФайлаВоВременномХранилище)
	ИмяВременногоФайла = ПолучитьИмяВременногоФайла(".feature");
	ОбъектОбработки = РеквизитФормыВЗначение("Объект");
	ТЗСбора = РеквизитФормыВЗначение("ТЗСбораТребований");
	Строка=ТЗСбора.Получить(ИндСтроки);
	ТД = Новый ТекстовыйДокумент;
	Шаблон=ОбъектОбработки.ПолучитьМакет(ИмяМакета);
	ОбластьШапка=Шаблон.ПолучитьОбласть("Шапка");
	ОбластьПостоянная=Шаблон.ПолучитьОбласть("Постоянная");
	ОбластьПеременная=Шаблон.ПолучитьОбласть("Переменная");
	ОбластьПостоянная.Параметры.Функционал = СокрЛП(Строка.Функционал);
	ОбластьПостоянная.Параметры.Роль = СокрЛП(Строка.Роль);
	ОбластьПостоянная.Параметры.Описание = СокрЛП(Строка.Описание);
	ОбластьПостоянная.Параметры.Цель = СокрЛП(Строка.Цель);
	ТД.Вывести(ОбластьШапка);
	ТД.Вывести(ОбластьПостоянная);
	КоличествоСценариев=?(Строка.КоличествоСценариев=0,1,Строка.КоличествоСценариев);
	Для кк=1 по КоличествоСценариев Цикл
		ТД.Вывести(ОбластьПеременная);
	КонецЦикла;
	ТД.Записать(ИмяВременногоФайла);
	
	Файл = Новый Файл(ИмяВременногоФайла);
	Если Файл.Существует() Тогда
		ДвоичныеДанные = Новый ДвоичныеДанные(ИмяВременногоФайла);
		АдресФайлаВоВременномХранилище = ПоместитьВоВременноеХранилище(ДвоичныеДанные, УникальныйИдентификатор);
		УдалитьФайлы(ИмяВременногоФайла);
	КонецЕсли;

КонецФункции	

&НаСервере
Процедура ОбходДереваРекурсивно(Дерево,СписокДляВыбора)
	Для Каждого Строка Из Дерево.Строки Цикл
		Если Строка.Компонента<>"step_definitions" и НЕ Строка.Фича Тогда
			СписокДляВыбора.Добавить(Строка.ПолныйПуть,Строка.Компонента);
		КонецЕсли;
		Если Строка.Строки.Количество()>0 Тогда
			ОбходДереваРекурсивно(Строка,СписокДляВыбора);
		КонецЕсли;
	КонецЦикла;
КонецПроцедуры	

&НаСервере 
Функция ПолучитьСписокДляВыбораИзДерева()
	СписокДляВыбора=Новый СписокЗначений;
	//СписокКомп = РеквизитФормыВЗначение("СписокКомпонент");
	СписокКомп = РеквизитФормыВЗначение("ОбъемПроекта");
	ОбходДереваРекурсивно(СписокКомп,СписокДляВыбора);
	
	Возврат Новый Структура("СписокДляВыбора",СписокДляВыбора);
КонецФункции	

&НаКлиенте
Процедура ПослеВыбораИзМеню(ВыбранныйЭлемент, Параметры) Экспорт
    // Обработка выбранного элемента
	Если ВыбранныйЭлемент<>Неопределено Тогда
		ЭтаФорма.ТекущийЭлемент.ТекущиеДанные.Компонент=ВыбранныйЭлемент.Представление;
		ЭтаФорма.ТекущийЭлемент.ТекущиеДанные.ПутьККомпоненте=ВыбранныйЭлемент.Значение;
	КонецЕсли;	
КонецПроцедуры

&НаКлиенте
Процедура ТЗСбораТребованийПриНачалеРедактирования(Элемент, НоваяСтрока, Копирование)
	// Вставить содержимое обработчика.
	Если НоваяСтрока Тогда
		Элемент.ТекущиеДанные.ФичаФайл="";
	КонецЕсли;	
КонецПроцедуры

&НаКлиенте
Процедура ОткрытиеФичаФайлаИзТЗСбораТребований(ПутьКФиче) Экспорт
	Если СокрЛП(ПутьКФиче)<>"" Тогда
		ФормаОбработки = ПолучитьФорму("ВнешняяОбработка.BDDEditor.Форма.ФормаРедактора",,ЭтаФорма);
		СписокОткрытыхОкон=ПолучитьОкна();
		Если ФормаОбработки.Открыта() Тогда
			Если ФормаОбработки.ПутьКФиче<>ПутьКФиче Тогда// И ФормаОбработки.БылиИзменения Тогда
				ФормаОбработки.ЗаписатьИзмененияВФиче();
				ФормаОбработки.ПутьКФиче=ПутьКФиче;
				ФормаОбработки.ЕстьПоддержкаАсинхронныхВызовов=ЭтаФорма.ЕстьПоддержкаАсинхронныхВызовов;
				ФормаОбработки.ЕстьПоддержкаНемодальныхФорм=ЭтаФорма.ЕстьПоддержкаНемодальныхФорм;
				ФормаОбработки.КаталогBDDEditor=Объект.КаталогBDDEditor;
				ФормаОбработки.ПолучитьТекстФичи();
				ФормаОбработки.Активизировать();
				ФормаОбработки.Открыть();
			Иначе	
				ФормаОбработки.ПутьКФиче=ПутьКФиче;
				ФормаОбработки.ЕстьПоддержкаАсинхронныхВызовов=ЭтаФорма.ЕстьПоддержкаАсинхронныхВызовов;
				ФормаОбработки.ЕстьПоддержкаНемодальныхФорм=ЭтаФорма.ЕстьПоддержкаНемодальныхФорм;
				ФормаОбработки.КаталогBDDEditor=Объект.КаталогBDDEditor;
				ФормаОбработки.ПолучитьТекстФичи();
				ФормаОбработки.Открыть();
			КонецЕсли;	
		Иначе	
			ФормаОбработки.ПутьКФиче=ПутьКФиче;
			ФормаОбработки.ЕстьПоддержкаАсинхронныхВызовов=ЭтаФорма.ЕстьПоддержкаАсинхронныхВызовов;
			ФормаОбработки.ЕстьПоддержкаНемодальныхФорм=ЭтаФорма.ЕстьПоддержкаНемодальныхФорм;
			ФормаОбработки.КаталогBDDEditor=Объект.КаталогBDDEditor;
			ФормаОбработки.Открыть();
		КонецЕсли;	
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ТЗСбораТребованийВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	// Вставить содержимое обработчика.
	Если Поле.Имя="ФичаФайл" Тогда
		СтандартнаяОбработка=Ложь;
		Если ЗначениеЗаполнено(Элемент.ТекущиеДанные.ФичаФайл) Тогда
			ОткрытиеФичаФайлаИзТЗСбораТребований(Элемент.ТекущиеДанные.ФичаФайл);
		КонецЕсли;
	ИначеЕсли Поле.Имя = "Компонент" Тогда
		СтандартнаяОбработка=Ложь;
		СписокВыбораКомпонент=Новый СписокЗначений;
		СписокВыбораКомпонент=ПолучитьСписокДляВыбораИзДерева()["СписокДляВыбора"];
		Если СписокВыбораКомпонент.Количество()>0 Тогда
			РежимМодальности=ПолучитьРежимМодальностиКонфигурации();
			Если НЕ РежимМодальности Тогда		
				Оповещение = Новый ОписаниеОповещения("ПослеВыбораИзМеню",ЭтотОбъект, );
				ПоказатьВыборИзМеню(Оповещение, СписокВыбораКомпонент,);
			Иначе
				ВыборКомпоненты=ВыбратьИзМеню(СписокВыбораКомпонент);
				Если ВыборКомпоненты<>Неопределено Тогда
					Элемент.ТекущиеДанные.Компонент=ВыборКомпоненты.Представление;
					Элемент.ТекущиеДанные.ПутьККомпоненте=ВыборКомпоненты.Значение;
				КонецЕсли;	
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;	
КонецПроцедуры

&НаКлиенте
Функция ЗаполнитьДеревоКомпонент(ПутьКаталога,МассивСпискаКомпонент,Уровень)
	Если ЕстьПоддержкаАсинхронныхВызовов Тогда
		НайденныеФайлы=Новый Структура;
		//Дописать	
	Иначе
    	НайденныеФайлы = НайтиФайлы(ПутьКаталога,"*.*",Истина);
	КонецЕсли;
	Для каждого Файл из НайденныеФайлы цикл
		Если Файл.ЭтоКаталог() И НЕ Файл.ПолучитьНевидимость() тогда
			СтруткураФайла = Новый Структура;
			СтруткураФайла.Вставить("Уровень",Уровень);
			СтруткураФайла.Вставить("Каталог",Истина);
			СтруткураФайла.Вставить("Имя",Файл.Имя);
			СтруткураФайла.Вставить("ПолныйПуть",Файл.ПолноеИмя);
			МассивСпискаКомпонент.Добавить(СтруткураФайла);
			Уровень=Уровень+1;
            ЗаполнитьДеревоКомпонент(ПутьКаталога+"\"+Файл.Имя,МассивСпискаКомпонент,Уровень);
			Уровень=Уровень-1;
        КонецЕсли;
    КонецЦикла;
КонецФункции

&НаКлиенте
Процедура ПолучитьСтруктуруКаталогов(ДеревоЗаполнения)
	ПутьККаталогам=Объект.СтруктураДляЗагрузки;
	ЭтаФорма[ДеревоЗаполнения].ПолучитьЭлементы().Очистить();
	МассивСпискаКомпонент=Новый Массив;
	//	Путь = Новый Файл(ПутьККаталогам);
	
	Если ЕстьПоддержкаАсинхронныхВызовов Тогда
		//Путь = Новый Файл();
		//Путь.НачатьИнициализацию(Новый ОписаниеОповещения("ПолучитьСтруктуруКаталоговЗавершение", ЭтаФорма, Новый Структура("МассивСпискаКомпонент, ПутьККаталогам", МассивСпискаКомпонент, ПутьККаталогам)), ПутьККаталогам);
		ОписаниеОповещения = Новый ОписаниеОповещения("ПолучитьСтруктуруКаталоговЗавершение",ЭтотОбъект,Новый Структура("МассивСпискаКомпонент, ПутьККаталогам, ДеревоЗаполнения", МассивСпискаКомпонент, ПутьККаталогам, ДеревоЗаполнения));//,"ОбработкаФайловОшибка",ЭтотОбъект);
		НачатьПоискФайлов(ОписаниеОповещения,ПутьККаталогам,"*.*",истина);
	Иначе	
		
		Путь = Новый Файл(ПутьККаталогам);
    	НайденныеФайлы = НайтиФайлы(ПутьККаталогам,"*.*",Истина);
		ДополнительныеПараметры=Новый Структура("МассивСпискаКомпонент, ПутьККаталогам, ДеревоЗаполнения", МассивСпискаКомпонент, ПутьККаталогам, ДеревоЗаполнения);
		ПолучитьСтруктуруКаталоговЗавершение(НайденныеФайлы, ДополнительныеПараметры)
	КонецЕсли;	
	
КонецПроцедуры

&НаКлиенте
Процедура ПолучитьСтруктуруКаталоговЗавершение(НайденныеФайлы, ДополнительныеПараметры) Экспорт
	
	МассивСпискаКомпонент = ДополнительныеПараметры.МассивСпискаКомпонент;
	ПутьККаталогам = ДополнительныеПараметры.ПутьККаталогам;
	ДеревоЗаполнения = ДополнительныеПараметры.ДеревоЗаполнения;
	Контекст = Новый Структура;
	Контекст.Вставить("Индекс",-1);
	Контекст.Вставить("Файлы",НайденныеФайлы);
	Контекст.Вставить("ДеревоЗаполнения",ДеревоЗаполнения);
	ЦиклОбработкиФайлов(Контекст);

КонецПроцедуры

&НаКлиенте
Процедура ЦиклОбработкиФайлов(Контекст) Экспорт
	Если Контекст.Индекс + 1 >= Контекст.Файлы.Количество() Тогда
//		ВыполнитьОбработкуОповещения(Контекст.оповещение,Истина);
		Возврат;
	КонецЕсли;
	
	Контекст.Индекс = Контекст.Индекс + 1;
	ОповещениеПроверки = Новый ОписаниеОповещения("ПослеПроверкиЭтоКаталог",ЭтотОбъект,Контекст);
	Контекст.Файлы[Контекст.Индекс].НачатьПроверкуЭтоКаталог(ОповещениеПроверки);
	
		
КонецПроцедуры

&НаКлиенте
Процедура ПослеПроверкиЭтоКаталог(ЭтоКаталог,Контекст) экспорт
	
	
	Если ЕстьПоддержкаАсинхронныхВызовов Тогда
		ОповещениеПроверки = Новый ОписаниеОповещения("ПослеПолученияВремениИзменения",ЭтотОбъект,Новый Структура("Контекст,ЭтоКаталог",Контекст,ЭтоКаталог));
		Контекст.Файлы[Контекст.Индекс].НачатьПолучениеВремениИзменения(ОповещениеПроверки);
	Иначе
		ВремяИзменения=Контекст.файлы[Контекст.Индекс].ПолучитьВремяИзменения();
		СтруктураДанных=Новый Структура("Путь,Имя,ПолноеИмя,Расширение",Контекст.файлы[Контекст.Индекс].Путь,Контекст.файлы[Контекст.Индекс].Имя,Контекст.файлы[Контекст.Индекс].ПолноеИмя,Контекст.Файлы[Контекст.Индекс].Расширение);
		ДобавитьВДерево(СтруктураДанных,ЭтоКаталог,Контекст.ДеревоЗаполнения,ВремяИзменения);
		ЦиклОбработкиФайлов(Контекст);
	КонецЕсли;	
КонецПроцедуры

&НаКлиенте
Процедура ПослеПолученияВремениИзменения(ВремяИзменения,ДополнительныеПараметры) Экспорт
	Контекст=ДополнительныеПараметры.Контекст;
	ЭтоКаталог=ДополнительныеПараметры.ЭтоКаталог;
	СтруктураДанных=Новый Структура("Путь,Имя,ПолноеИмя,Расширение",Контекст.файлы[Контекст.Индекс].Путь,Контекст.файлы[Контекст.Индекс].Имя,Контекст.файлы[Контекст.Индекс].ПолноеИмя,Контекст.Файлы[Контекст.Индекс].Расширение);
	ДобавитьВДерево(СтруктураДанных,ЭтоКаталог,Контекст.ДеревоЗаполнения,ВремяИзменения);
	ЦиклОбработкиФайлов(Контекст);
	
КонецПроцедуры	

&НаСервере
Процедура ДобавитьВДерево(СтруктураДанных,ЭтоКаталог,ДеревоЗаполнения,ВремяИзменения)
	Путь=СтруктураДанных.Путь;
	Имя=СтруктураДанных.Имя;
	ПолноеИмя=СтруктураДанных.ПолноеИмя;
	Расширение=СтруктураДанных.Расширение;
	СписокКомпонент1 = РеквизитФормыВЗначение(СокрЛП(ДеревоЗаполнения));
	Стр = СписокКомпонент1.Строки.Найти(Путь,"ПолныйПуть",истина);
	Если Стр = Неопределено Тогда
		Ветка = СписокКомпонент1.Строки.Добавить();
		Ветка.Компонента = "features";
		Ветка.ПолныйПуть = Путь;
		Ветка.Имя = "features";
		Ветка.ТипКартинки=0;
		Если СписокКомпонент1.Колонки.Найти("КоличествоФичаФайлов")<>Неопределено Тогда
			МассивКоличество=НайтиФайлы(Ветка.ПолныйПуть,"*.feature",Истина);
			Ветка.КоличествоФичаФайлов=МассивКоличество.Количество();
		КонецЕсли;
		Если СписокКомпонент1.Колонки.Найти("ПоследнееОбновление")<>Неопределено Тогда
			Ветка.ПоследнееОбновление=ВремяИзменения;
		КонецЕсли;
	Иначе
		Ветка = Стр;
	КонецЕсли;
	Стр = Ветка.строки.Найти(Имя,"Компонента",Истина);
	Если Стр = Неопределено Тогда
		Если ЭтоКаталог или Расширение=".feature" Тогда
		
			Ветка1 = ветка.Строки.Добавить();
			Ветка1.Компонента = Имя;
			Ветка1.ПолныйПуть = ПолноеИмя + ?(ЭтоКаталог,"\","");
			Ветка1.Имя = Имя;
			Если ЭтоКаталог Тогда
				Ветка1.ТипКартинки=0;
			Иначе
				Ветка1.Фича=Истина;
				Ветка1.ТипКартинки=1;
			КонецЕсли;
			Если СписокКомпонент1.Колонки.Найти("КоличествоФичаФайлов")<>Неопределено Тогда
				Если ЭтоКаталог Тогда
					МассивКоличество=НайтиФайлы(Ветка1.ПолныйПуть,"*.feature",Истина);
					Ветка1.КоличествоФичаФайлов=МассивКоличество.Количество();
				Иначе
					Ветка1.КоличествоФичаФайлов=1;
				КонецЕсли;
			КонецЕсли;
			Если СписокКомпонент1.Колонки.Найти("ПоследнееОбновление")<>Неопределено Тогда
				Ветка1.ПоследнееОбновление=ВремяИзменения;
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;

	ЗначениеВРеквизитФормы(СписокКомпонент1,СокрЛП(ДеревоЗаполнения));
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьДеревоЗначенийПоМассивуСервер(МассивСпискаКомпонент,СтруктураПараметров)
	СписокКомп = РеквизитФормыВЗначение("СписокКомпонент");
	
	СписокКомп.Строки.Очистить();
	ТекУровень = 0;
	ТекДерево = СписокКомп;
	
	Для каждого Элем Из МассивСпискаКомпонент Цикл
		Если Элем.Имя="step_definitions" Тогда
			Продолжить;
		КонецЕсли;	
		Если Элем.Уровень > ТекУровень Тогда
			ТекУровень = Элем.Уровень;
			НовСтр     = ТекДерево.Строки.Добавить();
			ТекДерево = НовСтр;
		ИначеЕсли Элем.Уровень < ТекУровень Тогда
			Разн = ТекУровень - Элем.Уровень;
			Для Ккк = 1 По Разн Цикл
				НовСтр = НовСтр.Родитель;
			КонецЦикла;
			НовСтр     = НовСтр.Родитель.Строки.Добавить();
			ТекУровень = Элем.Уровень;
			ТекДерево = НовСтр;
		Иначе	
			НовСтр     = ТекДерево.Родитель.Строки.Добавить();
		КонецЕсли;  
		
		НовСтр.Компонента = Элем.Имя;
		НовСтр.ПолныйПуть = Элем.ПолныйПуть;
		НовСтр.Имя        = Элем.Имя;
	КонецЦикла;
	
	ЗначениеВРеквизитФормы(СписокКомп,"СписокКомпонент");
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьСписок(Команда)
	 // Вставить содержимое обработчика.
	//ПолучитьСтруктуруКаталогов("СписокКомпонент");
	ПолучитьСтруктуруКаталогов("ОбъемПроекта");
КонецПроцедуры

#КонецОбласти

#Область Эпик

&НаСервере
Процедура СохранитьНастройкиВХранилище()
	Если Объект.РежимСамотестирования Тогда
		Возврат;
	КонецЕсли;  
	
	Настройки = Новый Структура;
	Настройки.Вставить("gitrepos", Объект.Репозиторий);
	
	ХранилищеОбщихНастроек.Сохранить("VanessaEpics",, Настройки);
	
КонецПроцедуры

&НаКлиенте
Процедура СохранитьНастройкиРепозитория(Команда)
	// Вставить содержимое обработчика.
	СохранитьНастройкиВХранилище();

КонецПроцедуры

&НаКлиенте
Процедура CохранитьЦелиИРоли(Команда)
	// Вставить содержимое обработчика.
	
КонецПроцедуры

&НаСервере
Процедура АктивироватьСтраницыНастройки()
	
	Элементы.НастройкиРепозитория.Видимость = Истина;
	Элементы.РаботаСЦелямиИКомпонентами.Видимость = Ложь;
	
КонецПроцедуры

Процедура АктивироватьСтраницыРаботыСРепозиторием()
	
	Элементы.НастройкиРепозитория.Видимость = Ложь;
	Элементы.РаботаСЦелямиИКомпонентами.Видимость = Истина;
	
КонецПроцедуры

&НаКлиенте
Процедура ПоказыватьРасшифровкуПриИзменении(Элемент)
	// Вставить содержимое обработчика.
	ЭтаФорма.Элементы.ТЗСбораТребованийРасшифровка.Видимость=Объект.ПоказыватьРасшифровку;
КонецПроцедуры

#КонецОбласти

#Область Статистика

&НаКлиенте
Процедура СобратьСтатистикуПоБизнесКомпонентам(Команда)
	// Вставить содержимое обработчика.
	ПолучитьСтруктуруКаталогов("ОбъемПроекта");
	
КонецПроцедуры

#КонецОбласти

#Область СтруктураКомпонент

&НаКлиенте
Процедура ШаблонФункциональныхТребованийНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	// Вставить содержимое обработчика.
	Режим = РежимДиалогаВыбораФайла.Открытие;
	ДиалогОткрытияФайла = Новый ДиалогВыбораФайла(Режим);
	ДиалогОткрытияФайла.Заголовок = "Выберите путь к обработке Vanessa Behavior ";
	ДиалогОткрытияФайла.Фильтр = "Файл (*.yml)|*.yml";
	Если ЕстьПоддержкаАсинхронныхВызовов Тогда
		ОписаниеОповещения = Вычислить("Новый ОписаниеОповещения(""ОбработатьВыборФайла"", ЭтаФорма,Новый Структура(""Путь"", ""ШаблонФункциональныхТребований""))");
		Выполнить("ДиалогОткрытияФайла.Показать(ОписаниеОповещения)");
	Иначе	
		Если ДиалогОткрытияФайла.Выбрать() Тогда
			Объект.ШаблонФункциональныхТребований = ДиалогОткрытияФайла.ПолноеИмяФайла;
		КонецЕсли;
	КонецЕсли;  
	
КонецПроцедуры

&НаКлиенте
Функция НайтиФайлИНИБК() Экспорт
	
КонецФункции	

&НаКлиенте
Процедура ЗагрузитьШаблонБК(Команда) Экспорт
	// Вставить содержимое обработчика.
	Если ЕстьПоддержкаАсинхронныхВызовов Тогда
		ОписаниеОповещения = Новый ОписаниеОповещения("ПроверитьНаличиеФайлаИНИ",ЭтаФорма,Новый Структура());//,"ОбработкаФайловОшибка",ЭтотОбъект);
		НачатьПоискФайлов(ОписаниеОповещения, Объект.СтруктураДляЗагрузки,"components.ini",Ложь);
	Иначе
		
		НайденныеФайлы = НайтиФайлы(Объект.СтруктураДляЗагрузки,"components.ini",Ложь);
		ПроверитьНаличиеФайлаИНИ(НайденныеФайлы,Новый Структура());
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПроверитьНаличиеФайлаИНИ(НайденныеФайлы,ДополнительныеПараметры) Экспорт
	
	Если НайденныеФайлы.Количество()>0 Тогда
		ФайлКомпоненты=НайденныеФайлы[0];
		ФайлКомпонентИНИСуществует(ФайлКомпоненты);
	Иначе
		ФайлКомпонентИНИНеСуществует();
	КонецЕсли;	
	
КонецПроцедуры 

&НаКлиенте
Процедура ФайлКомпонентИНИСуществует(ФайлКомпоненты) Экспорт
	
	
КонецПроцедуры	

&НаКлиенте
Процедура ФайлКомпонентИНИНеСуществует() Экспорт
	
	ИмяФайла = СтрРазделить(Объект.ШаблонФункциональныхТребований,"\",Символы.ПС);
	ИмяФайла = ИмяФайла[ИмяФайла.Количество()-1]; 
	ИмяКомпоненты=Новый ТекстовыйДокумент;
	ИмяКомпоненты.ДобавитьСтроку("components:"+ИмяФайла);
	
	ФайлСтруктуры=Новый ЧтениеJSON;
	ФайлСтруктуры.ОткрытьФайл(Объект.ШаблонФункциональныхТребований);
	СтруктураJSON=ПрочитатьJSON(ФайлСтруктуры);
	ФайлСтруктуры.Закрыть();
	
	ПутьСоздания=Объект.СтруктураДляЗагрузки;
	ПрочитатьСтруктуруJSON(СтруктураJSON,ПутьСоздания);
	
	ИмяКомпоненты.ДобавитьСтроку("версия"+":"+СтруктураJSON["версия"]);
	ИмяКомпоненты.ДобавитьСтроку("описание"+":"+СтруктураJSON["описание"]);
	
	ПутьКФайлу = Объект.СтруктураДляЗагрузки+"\components.ini";
	ИмяКомпоненты.Записать(ПутьКФайлу);
	
	
КонецПроцедуры

&НаКлиенте
Процедура ПрочитатьСтруктуруJSON(СтруктураJSON,ПутьСоздания) Экспорт
	Для каждого Стр из СтруктураJSON Цикл
		Если ТипЗнч(Стр.Значение)=Тип("Массив") Тогда
			Если Стр.Ключ<>"features" Тогда
				ПутьСозданияПодКаталога=Объект.СтруктураДляЗагрузки+"\"+Стр.Ключ;
			Иначе
				ПутьСозданияПодКаталога=Объект.СтруктураДляЗагрузки;
			КонецЕсли;
			Если Стр.Ключ<>"features" Тогда
				СоздатьКаталоги(Стр.Ключ,ПутьСоздания);
			КонецЕсли;	
			ПрочитатьМассивJSON(Стр.Ключ,Стр.Значение,ПутьСоздания);
		ИначеЕсли ТипЗнч(Стр.Значение)=Тип("Структура") Тогда
			Если Стр.Ключ="features" или Стр.Ключ="Каталоги" Тогда
				ПутьСоздания1=Объект.СтруктураДляЗагрузки;
			Иначе
				ПутьСоздания1=ПутьСоздания+"\"+Стр.Ключ;
			КонецЕсли;	
			ПрочитатьСтруктуруJSON(Стр.Значение,ПутьСоздания1);
		КонецЕсли;
	КонецЦикла	
	
КонецПроцедуры	

&НаКлиенте
Процедура ПрочитатьМассивJSON(Имя,МассивJSON,ПутьСозданияПодКаталога) Экспорт
	Для кк=0 по МассивJSON.Количество()-1 Цикл
		Стр=МассивJSON.Получить(кк);
		Если ТипЗнч(Стр)=Тип("Структура") Тогда
			Если Имя="features" Тогда ИмяКаталога="" Иначе ИмяКаталога=Имя КонецЕсли;
			ПрочитатьСтруктуруJSON(Стр,ПутьСозданияПодКаталога+"\"+ИмяКаталога);
		КонецЕсли;	
	КонецЦикла;	
КонецПроцедуры

&НаКлиенте
Процедура СоздатьКаталоги(НаименованиеКаталога,ПутьСозданияКаталога) Экспорт
	НовыйКаталог=Новый Файл(ПутьСозданияКаталога+"\"+НаименованиеКаталога);
	Если НЕ НовыйКаталог.Существует() Тогда
		СоздатьКаталог(НовыйКаталог.ПолноеИмя);
	КонецЕсли;	
	
КонецПроцедуры	

&НаКлиенте
Процедура СтруктураДляЗагрузкиОчистка(Элемент, СтандартнаяОбработка)
	// Вставить содержимое обработчика.
	ЭтаФорма.ТЗСбораТребований.Очистить();
	
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьVB(Команда) Экспорт
	
	ИмяОбработки = ПодключитьВнешнююОбработкуКлиент(Объект.ПутьКVanessaBehavior);
	ФормаОбработки = ПолучитьФорму("ВнешняяОбработка." + ИмяОбработки + ".Форма",,,Истина);
	Если ЕстьПоддержкаАсинхронныхВызовов Тогда
		ПоказатьЗначение(Новый ОписаниеОповещения("ОткрытьПолученнуюФорму", ЭтотОбъект, Новый Структура("ФормаОбработки", ФормаОбработки)), ФормаОбработки);		
		
	Иначе
		ФормаОбработки.Открыть();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьПолученнуюФорму(ДополнительныеПараметры) Экспорт
	
	ФормаОбработки = ДополнительныеПараметры.ФормаОбработки;
	
	ФормаОбработки.Открыть();

КонецПроцедуры

#КонецОбласти


#Область HistoryGIT

&НаКлиенте
Процедура ЗагрузитьИсториюGIT(Команда)
	// Вставить содержимое обработчика.
КонецПроцедуры

&НаКлиенте
Процедура СоздатьВременныйКаталогGIT(Существует, ДополнительныеПараметры) Экспорт
	
	ПутьКФайлу = ДополнительныеПараметры.ПутьКФайлу;
	ПутьКФайлуСТемп = ДополнительныеПараметры.ПутьКФайлуСТемп;
	КомандаGIT = ДополнительныеПараметры.КомандаGIT;
	Если НЕ Существует Тогда
		НачатьСозданиеКаталога(Новый ОписаниеОповещения("ВыполнитьКомандуGIT", ЭтаФорма, ДополнительныеПараметры), ПутьКФайлуСТемп);
		Возврат;
	Иначе		
		ВыполнитьКомандуGIT(КомандаGIT);		
	КонецЕсли;	

КонецПроцедуры


&НаКлиенте
Процедура СуществуетКаталогFeatures_not_in_gitСозданныйВоВременнойПапке(КомандаGIT) Экспорт
	ВременныйКаталог=КаталогВременныхФайлов();
	ПутьККаталогу=ВременныйКаталог+"features-not-in-git";
	Если ЕстьПоддержкаАсинхронныхВызовов Тогда
		Темп = Новый Файл();
		Темп.НачатьИнициализацию(Новый ОписаниеОповещения("ПроверитьНаличиеПапкиТемп", ЭтаФорма, Новый Структура("ПутьКФайлу, ПутьКФайлуСТемп, ДальнейшаяПроцедура,КомандаGIT", ВременныйКаталог, ПутьККаталогу,"СоздатьВременныйКаталогGIT",КомандаGIT)), ПутьККаталогу);
	Иначе
		Темп=Новый Файл(ПутьККаталогу);
		Если НЕ Темп.Существует() Тогда
			СоздатьКаталог(ПутьККаталогу);
		КонецЕсли;
		ВыполнитьКомандуGIT(КомандаGIT);
	КонецЕсли;	
	
КонецПроцедуры

&НаКлиенте
Процедура ЯВызываюКоманду(КомандаGIT) Экспорт
	ТекущаяСтрока=ЭтаФорма.Элементы.ОбъемПроекта.ТекущиеДанные;
	Если НЕ ТекущаяСтрока.Фича Тогда
		Возврат;
	КонецЕсли;	
	ПутьКФиче=ТекущаяСтрока.ПолныйПуть;
	КомандаGIT=СтрЗаменить(КомандаGIT,"ПутьКФиче",""""+ПутьКФиче+"""");
	СуществуетКаталогFeatures_not_in_gitСозданныйВоВременнойПапке(КомандаGIT);
	
КонецПроцедуры

&НаКлиенте
Процедура ВыполнитьКомандуGIT(КомандаGIT) Экспорт
	
	ВременныйКаталог=КаталогВременныхФайлов()+"features-not-in-git\";
	
	КомандаСистемы(КомандаGIT,Объект.КаталогBDDEditor);
	
	ПутьКФайлу=ВременныйКаталог+"History.txt";
	ТекстФайла = Новый ЧтениеТекста(ПутьКФайлу,КодировкаТекста.UTF8);
	Текст=ТекстФайла.Прочитать();
	ТекстовыеДанныеГИТ = Текст;
	
КонецПроцедуры	
	
&НаКлиенте
Процедура GITBlame(Команда)
	
	ВременныйКаталог=КаталогВременныхФайлов()+"features-not-in-git\";
	КомандаGIT="git blame ПутьКФиче >"""+ВременныйКаталог+"History.txt""";
	ЯВызываюКоманду(КомандаGIT);
	
КонецПроцедуры

&НаКлиенте
Процедура GITLog(Команда)
	
	ВременныйКаталог=КаталогВременныхФайлов()+"features-not-in-git\";
	//КомандаGIT="git log --pretty ПутьКФиче >"""+ВременныйКаталог+"History.txt""";
	//ЯВызываюКоманду(КомандаGIT);
	
	КомандаGIT="git log --pretty=format:""%h|%an|%ad|%s"" ПутьКФиче>"""+ВременныйКаталог+"History.txt""";
	ЯВызываюКоманду(КомандаGIT);
	
	РаспарситьТекстGITВТаблицу();
	
КонецПроцедуры

&НаКлиенте
Процедура GITLogStat(Команда)
	ВременныйКаталог=КаталогВременныхФайлов()+"features-not-in-git\";
	КомандаGIT="git log --stat --pretty=format:""%h|%an|%ad|%s"" ПутьКФиче >"""+ВременныйКаталог+"History.txt""";
	ЯВызываюКоманду(КомандаGIT);
//	РаспарситьТекстGITВТаблицу();
	
КонецПроцедуры

&НаКлиенте
Процедура GITCheckout(Команда)
	
КонецПроцедуры

&НаКлиенте
Процедура ПроверитьМестоХраненияТребований()
	ТекущаяСтрока=ЭтаФорма.Элементы.ОбъемПроекта.ТекущиеДанные;
	Если НЕ ТекущаяСтрока.Фича Тогда
		ЭтаФорма.Элементы.ГруппаВызововКомандГИТ.ТолькоПросмотр=Истина;
		ЭтаФорма.Элементы.GITBlame.Доступность=Ложь;
		ЭтаФорма.Элементы.GITCheckout.Доступность=Ложь;
		ЭтаФорма.Элементы.GITLog.Доступность=Ложь;
		ЭтаФорма.Элементы.GITLogStat.Доступность=Ложь;
		Возврат;
	КонецЕсли;	
	ВременныйКаталог=КаталогВременныхФайлов()+"features-not-in-git\";
	ПутьКФиче=ТекущаяСтрока.ПолныйПуть;
	КомандаGIT="git branch """+ПутьКФиче+""" >"""+ВременныйКаталог+"History.txt""";
//	Вернулось=0;
//	ЗапуститьПриложение("cmd /A /C "+КомандаGIT,,Истина,Вернулось);
	КомандаСистемы(КомандаGIT);
	
	ПутьКФайлу=ВременныйКаталог+"History.txt";
	ТекстФайла = Новый ЧтениеТекста(ПутьКФайлу,КодировкаТекста.UTF8);
	Текст=ТекстФайла.Прочитать();
	Если СтрНайти(Текст,"fatal: Not a git repository")>0 Тогда
		ЭтаФорма.Элементы.ГруппаВызововКомандГИТ.ТолькоПросмотр=Истина;
		ЭтаФорма.Элементы.GITBlame.Доступность=Ложь;
		ЭтаФорма.Элементы.GITCheckout.Доступность=Ложь;
		ЭтаФорма.Элементы.GITLog.Доступность=Ложь;
		ЭтаФорма.Элементы.GITLogStat.Доступность=Ложь;
	Иначе
		ЭтаФорма.Элементы.ГруппаВызововКомандГИТ.ТолькоПросмотр=Ложь;
		ЭтаФорма.Элементы.GITBlame.Доступность=Истина;
		ЭтаФорма.Элементы.GITCheckout.Доступность=Истина;
		ЭтаФорма.Элементы.GITLog.Доступность=Истина;
		ЭтаФорма.Элементы.GITLogStat.Доступность=Истина;
	КонецЕсли;	
КонецПроцедуры

&НаКлиенте 	
Процедура ДоступностьИсторииGIT()
	ТекущаяСтрока=ЭтаФорма.Элементы.ОбъемПроекта.ТекущиеДанные;
	Если НЕ ТекущаяСтрока.Фича Тогда
		ЭтаФорма.Элементы.ГруппаВызововКомандГИТ.ТолькоПросмотр=Истина;
		ЭтаФорма.Элементы.GITBlame.Доступность=Ложь;
		ЭтаФорма.Элементы.GITCheckout.Доступность=Ложь;
		ЭтаФорма.Элементы.GITLog.Доступность=Ложь;
		ЭтаФорма.Элементы.GITLogStat.Доступность=Ложь;
	Иначе
		ЭтаФорма.Элементы.ГруппаВызововКомандГИТ.ТолькоПросмотр=Ложь;
		ЭтаФорма.Элементы.GITBlame.Доступность=Истина;
		ЭтаФорма.Элементы.GITCheckout.Доступность=Истина;
		ЭтаФорма.Элементы.GITLog.Доступность=Истина;
		ЭтаФорма.Элементы.GITLogStat.Доступность=Истина;
	КонецЕсли;	
	ТабличныеДанныеГИТ.Очистить();	
	ТекстовыеДанныеГИТ="";
	
КонецПроцедуры

&НаКлиенте
Процедура ОбъемПроектаПриАктивизацииСтроки(Элемент)
	ДоступностьИсторииGIT();
	//ПроверитьМестоХраненияТребований();
КонецПроцедуры

&НаКлиенте
Процедура РазложитьСтрокуНаКолонки(Строка)
	Стр=СтрЗаменить(Строка,"|",Символы.ПС);
	СтрокаТЧ=ТабличныеДанныеГИТ.Добавить();
	Для КК = 1 По СтрЧислоСтрок(Стр) Цикл
		Стр1=СтрПолучитьСтроку(Стр,КК);		
		Если КК=1 Тогда
			СтрокаТЧ.Хеш=СокрЛП(Стр1);
		ИначеЕсли КК=2 Тогда
			СтрокаТЧ.Автор=СокрЛП(Стр1);
		ИначеЕсли КК=3 Тогда
			СтрокаТЧ.ДатаАвтора=СокрЛП(Стр1);
		ИначеЕсли КК=4 Тогда
			СтрокаТЧ.Содержание=СокрЛП(Стр1);
		КонецЕсли;	
	КонецЦикла;	
	
КонецПроцедуры	

&НаКлиенте
Процедура РаспарситьТекстGITВТаблицу()
	ТабличныеДанныеГИТ.Очистить();
	Для КК = 1 По СтрЧислоСтрок(ТекстовыеДанныеГИТ) Цикл
		Строка=СтрПолучитьСтроку(ТекстовыеДанныеГИТ,КК);		
		РазложитьСтрокуНаКолонки(Строка);
	КонецЦикла;	
	

КонецПроцедуры

#КонецОбласти
 
